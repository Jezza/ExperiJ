repositories {
    mavenCentral()
}

apply plugin: 'java'
apply plugin: 'maven'
apply plugin: 'signing'

group = "com.experij"
archivesBaseName = "experij"
version = "0.4.1"

dependencies {
	testCompile 'org.junit.jupiter:junit-jupiter-api:5.0.0-M3'
	
	testRuntime 'org.junit.platform:junit-platform-console:1.0.0-M3'
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.0.0-M3'
}

jar {
	version = project.version
	manifest {
		attributes("Manifest-Version": "1.0", "Agent-Class": "com.experij.ExperiJ", "Premain-Class": "com.experij.ExperiJ")
	}
}

signing {
    sign configurations.archives
}

task runJupiter(type: JavaExec) {
	systemProperty "experij.debug", "true"
    jvmArgs "-ea"
	jvmArgs "-javaagent:" + jar.archivePath
	jvmArgs "-Dexperij.debug=true"
	jvmArgs "-Dexperij.whitelist=\"com.experij\""
    classpath = project.sourceSets.test.runtimeClasspath
    main 'org.junit.platform.console.ConsoleLauncher'
    args '--scan-class-path'
    args '--hide-details'
    args "--reports-dir=$project.testReportDir"
}

test.dependsOn runJupiter

javadoc {
	// Just temp until I get around to fixing those javadoc errors. ;)
	failOnError = false
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

uploadArchives {
	repositories {
		mavenDeployer {
			beforeDeployment {
				MavenDeployment deployment -> signing.signPom(deployment)
			}
			
			repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
				authentication(userName: ossrhUsername, password: ossrhPassword)
			}
			
			pom.project {
				name 'ExperiJ'
				packaging 'jar'
				description 'A fast and customisable runtime experiment manager for Java.'
				url 'experij.com'
				
				scm {
					url 'https://github.com/Jezza/ExperiJ'
					connection 'scm:git:git://github.com/Jezza/ExperiJ.git'
					developerConnection 'scm:git:git@github.com:Jezza/ExperiJ.git'
				}
				
				licenses {
					license {
						name 'GNU Lesser General Public License v3 (LGPL-3.0)'
						url 'https://www.gnu.org/licenses/lgpl-3.0.txt'
					}
				}
				
				developers {
					developer {
						id 'jezza'
						name 'Jezza'
						email 'jeremy.a.barrow@gmail.com'
					}
				}
			}
		}
	}
}
